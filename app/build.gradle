import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

tasks.whenTaskAdded { task ->
    // Build rclone beforehand.
    if (task.name.startsWith('compile')) {
        task.dependsOn(':rclone:buildAll')
    }
}

android {
    namespace 'ca.pkay.rcloneexplorer'

    signingConfigs {
        release {
            keyAlias 'fdroid'
            storeFile file('.config/android/roundsync.keystore')
        }
    }
    compileSdkVersion 34
    ndkVersion project.properties['de.felixnuesse.extract.ndkVersion']
    defaultConfig {
        generatedDensities = []
        vectorDrawables.generatedDensities = []
        applicationId 'de.felixnuesse.extract'
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 240 // last digit is reserved for ABI, only ever end on 0!
        versionName '2.1.4'
        resourceConfigurations += ['en', 'de']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "java.lang.String", "CLI", System.getenv('RCX_BUILD_CLI') ? System.getenv('RCX_BUILD_CLI') : "\"c03129b6-b09f-9cb4-8fcd-7f143b8f94ef\""
        buildConfigField "java.lang.String", "VCP_AUTHORITY", "\"" + applicationId + ".vcp\"";
        setProperty("archivesBaseName",  "roundsync_v"+versionName)

        externalNativeBuild {
            ndkBuild {
                arguments "LOCAL_LDFLAGS += -Wl,--build-id=none"
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        all {
            jniLibs.srcDirs = ["lib"]
        }
    }

    flavorDimensions 'edition', 'abi'
    productFlavors {
        // Default GitHub and Google Play build
        rcx {
            dimension 'edition'
        }
        // OSS build
        oss {
            dimension 'edition'
        }
        // personal build
        arm64 {
            dimension 'abi'
            versionNameSuffix "-ts"
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }

    splits {
        abi {
            enable false
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    project.ext.versionCodes = [
        'armeabi-v7a': 6,
        'arm64-v8a'  : 7,
        'x86'        : 8,
        'x86_64'     : 9
    ]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }
    namespace 'ca.pkay.rcloneexplorer'


}

def now() {
    SimpleDateFormat df = new SimpleDateFormat ("yyyMMdd_hhmm")
    return df.format(new Date())
}

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0-alpha03'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0-alpha11'
    implementation 'androidx.browser:browser:1.6.0-rc01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.leinardi.android:speed-dial:3.3.0'
    implementation 'org.markdownj:markdownj-core:0.4'
    implementation 'jp.wasabeef:recyclerview-animators:4.0.2'
    implementation 'com.github.GrenderG:Toasty:1.3.0'

    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.datastore:datastore-preferences:1.1.0-alpha04'


    // Thumbnails
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
    // REST Client
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.11'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11'
    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    // Timestmps
    implementation 'com.github.x0b:rfc3339parser:2.0.0'
    // SAF/WebDAV
    implementation project(':safdav')
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    // Java 8+ library support
    // TODO: replace with coreLibraryDesugar once available
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.4'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.4'
    // App intro
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    // TEST
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
